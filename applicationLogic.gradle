apply plugin: 'application'

dependencies {
    runtime project(':marytts-runtime')
    runtime project(':marytts-languages').subprojects
    runtime project(':voice-cmu-slt-hsmm')
}

mainClassName = 'marytts.server.Mary'

run {
    systemProperties << ['mary.base': buildDir]
    doFirst {
        classpath += fileTree(buildDir).include('lib/*.jar')
    }
}

task copyComponentXmlDescriptors(type: Copy) {
    from "${project(':marytts-assembly:assembly-runtime').projectDir}/src/runtime/installed"
    into "$buildDir/installed"
    include '*.xml'
    rename 'component', "$version-component"
    expand properties
}

task copyComponentXml(type: Copy) {
    from 'download'
    into "$buildDir/download"
}

task runInstallerGui(type: JavaExec) {
    dependsOn copyComponentXmlDescriptors, copyComponentXml
    group 'Application'
    description 'Runs the MaryTTS Installer GUI as a JVM application'
    classpath run.classpath
    main 'marytts.tools.install.InstallerGUI'
    systemProperties << ['mary.base': buildDir]
}

distributions {
    main {
        contents {
            from copyComponentXmlDescriptors, {
                into 'installed'
            }
            from 'download', {
                into 'download'
            }
        }
    }
}

distTar {
    compression Compression.BZIP2
    extension 'tar.bz2'
}

startScripts {
    applicationName = 'marytts-server'
}

task installerGuiStartScripts(type: CreateStartScripts) {
    applicationName = 'marytts-component-installer'
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath
    mainClassName = 'marytts.tools.install.InstallerGUI'
    startScripts.finalizedBy it
}

tasks.withType(CreateStartScripts) {
    group 'Distribution'
    description startScripts.description
    doLast {
        // hack mary.base property into DEFAULT_JVM_OPTS
        unixScript.text = unixScript.text.replaceAll('DEFAULT_JVM_OPTS=""', 'DEFAULT_JVM_OPTS="-Dmary.base=\\$APP_HOME"')
        windowsScript.text = windowsScript.text.replaceAll('set DEFAULT_JVM_OPTS=', 'set DEFAULT_JVM_OPTS="-Dmary.base=%APP_HOME%"')
        // hack classpath
        unixScript.text = unixScript.text.replaceAll('-classpath "\\$CLASSPATH"', '-classpath "\\$APP_HOME/lib/*"')
        windowsScript.text = windowsScript.text.replaceAll('-classpath "%CLASSPATH%"', '-classpath "%APP_HOME%\\\\lib\\\\*"')
    }
}
