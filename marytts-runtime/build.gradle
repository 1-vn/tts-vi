plugins {
    id 'java'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

configurations {
    integrationTestCompile {
        extendsFrom testCompile
    }
    integrationTestRuntime {
        extendsFrom testRuntime
    }
}

apply from: "$rootDir/dependencies.gradle"

dependencies {
    compile project(':marytts-common')
    compile project(':marytts-signalproc')
    compile libs.commonsCollection
    compile libs.emotionmlCheckerJava
    compile libs.fastMd5
    compile libs.guava
    compile libs.hsqldb
    compile libs.httpCore
    compile libs.httpNio
    compile libs.icu4j
    compile libs.jtokCore
    compile libs.opennlpTools
    compile libs.trove4j
    testCompile project(path: ':marytts-common', configuration: 'testCompile')
    testCompile libs.festAssert
}

task integrationTest(type: Test) {
    group 'Verification'
    description 'Runs the integration tests.'
    check.dependsOn it
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}
